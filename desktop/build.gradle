plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'com.github.johnrengelman.shadow'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

mainClassName = "ai.arcblroth.boss.desktop.DesktopMain"

configurations {
    include
    backend
}

dependencies {
    include(implementation(project(':core')))
    backend(project(':roast'))

    // Backend loader
    include(implementation("org.scijava:native-lib-loader:$native_lib_loader_version"))
    include(implementation("org.slf4j:slf4j-jdk14:$slf4j_jdk_binding_version"))
}

processResources {
    from(configurations.backend.getFiles()) {
        into "natives/windows_64"
    }
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

shadowJar {
    archiveBaseName = 'BosstrovesRevenge'
    archiveClassifier = 'desktop'
    configurations = [project.configurations.include]
}

assemble.dependsOn shadowJar